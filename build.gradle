plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
}

loom {
    accessWidenerPath = file("src/main/resources/keymap.accesswidener")
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}-${project.mod_version_type}.${project.mod_subversion}+${project.minecraft_version}-${project.mod_version_loader.toLowerCase()}"
group = project.maven_group

repositories {
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://maven.terraformersmc.com" }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    flatDir {
        dirs "./lib"
    }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Config
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.version_cloth_api}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "com.terraformersmc:modmenu:${project.version_mod_menu}"

    // Malilib
    modImplementation name: "malilib-fabric-${project.version_malilib}"

    // Malilib Testing
    modRuntimeOnly name: "litematica-fabric-${project.version_litematica}"
    modRuntimeOnly name: "tweakeroo-fabric-${project.version_tweakeroo}"
    modRuntimeOnly name: "minihud-fabric-${project.version_minihud}"

    // Performance
    modRuntimeOnly "maven.modrinth:lithium:${project.version_lithium}"
    modRuntimeOnly "maven.modrinth:phosphor:${project.version_phosphor}"
    modRuntimeOnly "maven.modrinth:ferrite-core:${project.version_ferrite_core}"
    modRuntimeOnly "maven.modrinth:lazydfu:${project.version_lazydfu}"
    modRuntimeOnly "maven.modrinth:ksyxis:${project.version_ksyxis}"
    // modRuntimeOnly "maven.modrinth:c2me-fabric:${project.version_c2me}"
    modRuntimeOnly "maven.modrinth:notenoughcrashes:${project.version_nec}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java { withSourcesJar() }

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {}
}
